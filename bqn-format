#!/usr/bin/env bqn

c  ⇐ •Import "deps/color.bqn"
s  ⇐ •Import "deps/string.bqn"
fs ⇐ •Import "deps/file_system.bqn"
fn ⇐ •Import "deps/fun.bqn"

Align ← { w𝕊𝕩:
    n ← ⌈´{+´∨`⌾⌽' '≠⊑w s.Split𝕩}¨𝕩
    {(' '∾w∾' ')s.Join s.Trim¨⌾(1⊸↓) n⊸↑⌾⊑w s.Split𝕩}¨𝕩
}

Va ← {∧´𝕩∊"⟨⟩_, "∾∾"0Aa"+⟜↕¨10‿26‿26}                       # varaible or array
Aa ← {({⊑Va¨(1↑"←"⊸s.Split)𝕩}∧(=´·+´˘"{}"=⌜⊢)∧(∨´"←"∊˜⊢))𝕩} # assignment alignable
Ca ← {(∨´'#'=𝕩)∧∨´' '≠(∧`'#'⊸≠)⊸/𝕩}                         # comment alignable

FormatSingleFile ← {
    l ← •file.Lines 𝕩
    l ↩ {(¬·∧`⌾⌽' '⊸=)⊸/𝕩}¨l            # remove trailing whitespace
    g ← Aa fn._ChunkKey⊸⊔ l             # create chunks for aligning ←
    l ↩ ∾{ Aa⊑𝕩 ? "←" Align 𝕩 ⋄ ; 𝕩 }¨g # align ← symbols
    g ↩ Ca fn._ChunkKey⊸⊔ l             # create chunks for aligning #
    l ↩ ∾{ Ca⊑𝕩 ? "#" Align 𝕩 ⋄ ; 𝕩 }¨g # align # symbols
    𝕩 •file.Lines l
}

main ← {
    (c.Red "Must provide directory") ! 0≠≠•args
    d ← { "."≡0⊑•args ? •wdpath ; •wdpath∾"/"∾0⊑•args } # directory or file
    { 'f'≡•file.Type d ? FormatSingleFile d 
                       ; FormatSingleFile¨ fs.ListRecursiveBQNFiles d }
    •Out "All files formatted! 🥳"
}
